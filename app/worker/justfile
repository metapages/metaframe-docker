# Always use locally installed tools (always pin versions)
set shell                          := ["bash", "-c"]
set dotenv-load                    := true
# export SERVER_ORIGIN  := env_var_or_default("SERVER_ORIGIN", "http://server:8080")
export CI                          := env_var_or_default("CI", "")
###############################################################################
export DOCKER_IMAGE_PREFIX         := env_var_or_default("DOCKER_IMAGE_PREFIX", `which deno >/dev/null && which git >/dev/null && GITHUB_ACTIONS=true deno run --unstable --allow-all https://deno.land/x/metapages@v0.0.17/commands/docker_image_prefix.ts || echo ""`)
export DOCKER_TAG                  := env_var_or_default("DOCKER_TAG", `(which git >/dev/null && git rev-parse --short HEAD) || echo local`)
###############################################################################
NPM_MODULE            := `cat package.json | jq -r .name`
NPM_TOKEN             := env_var_or_default("NPM_TOKEN", "")
tsc                   := if path_exists("node_modules/typescript/bin/tsc") == "true" { "node_modules/typescript/bin/tsc" } else { "../node_modules/typescript/bin/tsc" }
# minimal formatting, bold is very useful
bold                  := '\033[1m'
normal                := '\033[0m'

@_help:
    just --list --unsorted --list-heading $'Commands:\n'
    echo -e ""
    echo -e "    Example command to start a worker using a published docker image:"
    echo -e ""
    echo -e "        docker run --restart unless-stopped -tid -v /var/run/docker.sock:/var/run/docker.sock -v /tmp:/tmp metapage/asman-worker:$(cat package.json | jq -r '.version') --cpus=4 --queue=public1"
    echo -e ""
    echo -e "    Example command to start a worker using local development code"
    echo -e ""
    echo -e "        docker-compose run --no-deps --rm worker just dev --cpus=1 --queue=local1 --server=http://server:8080"
    echo -e "        docker-compose run --no-deps --rm worker just dev --cpus=1 --queue=local1 --server=https://docker-metapage-io.glitch.me"
    echo -e ""

# Build the production code
build: clean _tsc
    cp package.json ./dist/
    cp -r bin ./dist/

# Run in development mode (automatic reloading)
dev +args="--cpus=1 --queue=local1": _npm_i
    @# ts-node-dev does not work with typescript project references https://github.com/TypeStrong/ts-node/issues/897
    watchexec --restart --watch ./src --watch ./justfile --watch ./package.json --watch ./tsconfig.json --watch ../shared/tsconfig-base.json --watch ../shared/src -- just run --id=local-dev "{{args}}"

# Build then run node script
run +args="": build
    @# Not using ts-node because https://github.com/TypeStrong/ts-node/issues/1007
    @# --experimental-specifier-resolution=node: https://stackoverflow.com/questions/64453859/directory-import-is-not-supported-resolving-es-modules-with-node-js
    node --experimental-specifier-resolution=node ./dist/bin/index.js {{args}}

# Actually run a worker docker image
worker queue="local1":
    cd .. && docker-compose -f docker-compose.yml build worker
    cd .. && docker-compose -f docker-compose.yml run --no-deps worker --cpus=1 --queue={{queue}} --server=https://docker-metapage-io.glitch.me/

# Publish npm module
publish: (_require "NPM_TOKEN")
    #!/usr/bin/env bash
    set -euo pipefail
    if [ "$CI" != "true" ]; then
        # This check is here to prevent publishing if there are uncommitted changes, but this check does not work in CI environments
        # because it starts as a clean checkout and git is not installed and it is not a full checkout, just the tip
        if [[ $(git status --short) != '' ]]; then
            git status
            echo -e 'üí• Cannot publish with uncommitted changes'
            exit 2
        fi
    fi
    VERSION=$(cat package.json | jq -r '.version')
    INDEX=`npm view {{NPM_MODULE}} versions --json | jq "index( \"$VERSION\" )"`
    if [ "$INDEX" != "null" ]; then
        echo -e 'üå≥ Version exists, not publishing'
        exit 0
    fi
    just build
    echo "PUBLISHING npm version $VERSION"
    cd ./dist && \
        echo "//registry.npmjs.org/:_authToken=${NPM_TOKEN}" > .npmrc && \
        npm publish --access public .

# Unpublish version https://docs.npmjs.com/cli/v7/commands/npm-unpublish
unpublish version:
    @echo "‚ùó If this fails: you cannot use .npmrc or NPM_TOKEN, you must 'npm login' ü§∑‚Äç‚ôÄÔ∏è"
    npm unpublish {{NPM_MODULE}}@{{version}}

# Push docker image to metapage/asman-worker. This is just for prototyping
publish_docker: (_require "DOCKER_TOKEN")
    #!/usr/bin/env bash
    set -euo pipefail
    echo "$DOCKER_TOKEN" | docker login --username metapage --password-stdin
    cd ../
    DOCKER_TAG=$(cat worker/package.json | jq -r '.version') docker-compose -f docker-compose.yml build
    DOCKER_TAG=$(cat worker/package.json | jq -r '.version') docker-compose -f docker-compose.yml push worker
    echo "‚úÖ ${DOCKER_IMAGE_PREFIX}worker:$(cat worker/package.json | jq -r '.version')"

# I don't just remove 'dist/' itself because it might be moutned into a docker-compose container
@clean:
    rm -rf dist/*
    rm -rf tsconfig.tsbuildinfo

# compile typescript src, may or may not emit artifacts
_tsc +args="": _npm_i
    {{tsc}} {{args}}

@_require env:
    if [ -z "{{env}}" ]; then echo "Missing {{env}}"; exit 1; fi

_npm_i:
    npm i